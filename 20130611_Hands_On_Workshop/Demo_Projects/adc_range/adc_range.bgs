# ================================================================
# BGScript demo: ADC range demonstration, from 0 to VDD
# Jeff Rowberg <jeff.rowberg@bluegiga.com>
# ----------------------------------------------------------------
#
# CHANGELOG:
#   2013-04-03 - Initial release
#
# Use P0_6 as the analog input. On the DKBLE112, this pin is wired
# to the potentiometer on the board, allowing for easy testing.
# You can disable the potentiometer and instead connect the pin
# directly to either P0_0 (output set LOW/GND) or to P0_1 (output
# set HIGH/VDD).
#
# ================================================================

event system_boot(major, minor, patch, build, ll_version, protocol_version, hw)
    # set P0_0 and P0_1 to output mode, one low (P0_0) and one high (P0_1)
    # (use these for easy reference of GND and VDD readings)
	call hardware_io_port_config_direction(0, $3)
	call hardware_io_port_write(0, $3, $2)

    # begin advertising at boot
    call gap_set_mode(gap_general_discoverable, gap_undirected_connectable)

    # enable 1-second repeating timer
	call hardware_set_soft_timer(32768, 0, 0)
end

event connection_disconnected(handle, result)
    # connection disconnected, resume advertising
    call gap_set_mode(gap_general_discoverable, gap_undirected_connectable)
end

event hardware_soft_timer(handle)
    # trigger ADC read on P0_6, full 12-bit decimation, AVDD reference
    call hardware_adc_read(6, 3, 2)
end

event hardware_adc_result(input, value)
    # write 2 bytes of ADC data to GATT characteristic
    call attributes_write(c_adc_data, 0, 2, value)
end